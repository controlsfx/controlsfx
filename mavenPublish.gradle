apply plugin: 'maven-publish'
apply plugin: 'signing'

signing {
    required = { false }
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            afterEvaluate {
                artifact sourceJar
                artifact javadocJar
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'ControlsFX'
                description = 'High quality UI controls and other tools to complement the core JavaFX distribution'
                url = 'http://www.controlsfx.org/'
                licenses {
                    license {
                        name = 'The 3-Clause BSD License'
                        url = 'http://www.opensource.org/licenses/bsd-license.php'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        name = 'James Richmond'
                        email = 'james.b.richmond@gmail.com'
                        roles = [
                                'author',
                                'developer'
                        ]
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/JimmyBoomBots3000/controlsfx'
                    developerConnection = 'scm:git:ssh://hg@github.com/JimmyBoomBots3000/controlsfx'
                    url = 'https://github.com/JimmyBoomBots3000/controlsfx'
                }

                // Remove classifier for JavaFX dependencies
                withXml {
                    Node pomNode = asNode()
                    pomNode.dependencies.'*'.findAll() {
                        it.groupId.text() == 'org.openjfx'
                    }.each {
                        it.parent().remove(it)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            allowInsecureProtocol = true
            name = 'internal'
            url = uri('http://hera.sesco.com:10082/repository/internal/')
            credentials {
                username = project.findProperty("repoUser")
                password = project.findProperty("repoPassword")
            }
        }
    }
}
