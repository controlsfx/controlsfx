import java.time.Duration

apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'de.marcphilipp.nexus-publish'

signing {
    required = { !version.endsWith("SNAPSHOT") }
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            afterEvaluate {
                artifact sourceJar
                artifact javadocJar
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'ControlsFX'
                description = 'High quality UI controls and other tools to complement the core JavaFX distribution'
                url = 'http://www.controlsfx.org/'
                licenses {
                    license {
                        name = 'The 3-Clause BSD License'
                        url = 'http://www.opensource.org/licenses/bsd-license.php'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        name = 'Jonathan Giles'
                        email = 'jonathan@jonathangiles.net'
                        roles = [
                                'author',
                                'developer'
                        ]
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/controlsfx/controlsfx'
                    developerConnection = 'scm:git:ssh://hg@github.com/controlsfx/controlsfx'
                    url = 'https://github.com/controlsfx/controlsfx'
                }

                // Remove classifier for JavaFX dependencies
                withXml {
                    Node pomNode = asNode()
                    pomNode.dependencies.'*'.findAll() {
                        it.groupId.text() == 'org.openjfx'
                    }.each {
                        it.parent().remove(it)
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = project.hasProperty('sonatypeUsername') ? project.property('sonatypeUsername') : ''
            password = project.hasProperty('sonatypePassword') ? project.property('sonatypePassword') : ''
        }
    }
    clientTimeout = Duration.ofMinutes(5)
    connectTimeout = Duration.ofMinutes(5)
}
